plugins {
    id 'maven-publish'
    id 'java-library'
}


configurations {
    include
    implementation.extendsFrom(include)
}

repositories {
    mavenCentral()
    mavenLocal()
}

Properties props = new Properties()
try (FileInputStream fis = new FileInputStream(new File(parent.rootDir.getPath(), "application.properties"))) {
    props.load(fis)
}

tasks.register("sourcesJar", Jar) {
    archiveClassifier.set("sources")
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
    archiveBaseName = project.plugin_name

    from {
        configurations.include.filter {
            parent.subprojects.buildDir.path.any { it2 -> it.path.startsWith(it2) }
        }.collect { it2 -> file(it2.path.replace(".jar", "-sources.jar")) }.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

tasks.register("shadowJar", Jar) {
    archiveClassifier.set("")
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
    archiveBaseName = project.plugin_name

    from {
        configurations.include.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

java {
    withSourcesJar()
}

jar.enabled = true

publishing {
    repositories {
        maven {
            name = "gpr"
            url = uri("https://maven.pkg.github.com/" + (project.findProperty("gr") ?: System.getenv("GITHUB_REPOSITORY")))
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GPR_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GPR_TOKEN")
            }
        }
    }

    publications {
        maven(MavenPublication) {
            groupId = props.getProperty("group")
            artifactId = props.getProperty("artifact")
            version = props.getProperty("version")

            artifact(tasks.named("sourcesJar")) {
                classifier "sources"
                extension "jar"
            }
            artifact(tasks.named("shadowJar")) {
                classifier ""
                extension "jar"
            }
        }
    }
}

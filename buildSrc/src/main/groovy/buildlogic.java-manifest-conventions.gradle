import java.time.ZoneOffset
import java.time.ZonedDateTime

plugins {
    id 'application'
}

Properties props = new Properties()
try (FileInputStream fis = new FileInputStream(new File(parent.rootDir.getPath(), "application.properties"))) {
    props.load(fis)
}

String date = ZonedDateTime.now(ZoneOffset.UTC).getDateTimeString()

jar {
    manifest {
        attributes 'Main-Class': 'net.akazukin.bot.Main',
                'Implementation-Title': props.getProperty("name"),
                'Created-By': String.valueOf(JavaVersion.current()) + " (" + props.getProperty("vendor") + ")",
                'Signature-Version': props.getProperty("version"),
                'Implementation-Date': date
    }
}

String commit = 'git rev-parse --verify --short HEAD'.execute().text.trim()
String commitHash = 'git rev-parse --verify HEAD'.execute().text.trim()

processResources {
    inputs.property 'version', props.getProperty("version")
    inputs.property 'vendor', props.getProperty("vendor")
    inputs.property 'commit', commit
    inputs.property 'commitHash', commitHash
    inputs.property 'buildDate', date

    filesMatching('buildData.properties') {
        expand([
                version   : props.getProperty("version"),
                vendor    : props.getProperty("vendor"),
                commit    : commit,
                commitHash: commitHash,
                buildDate : date
        ])
    }
}

tasks.withType(Jar).configureEach {
    manifest = jar.manifest
}

application {
    mainClassName = "net.akazukin.library.Main"
}